#!/usr/bin/env zsh

# Location of Zsh configuration files
export DOTFILES='{{ playbook_dir }}/roles/dotfiles/files'

# Directory for storing development projects that can be `c [tab]` to
export PROJECTS='{{ directories.dev_projects }}'

# Default editor
export EDITOR='{{ default_editor.shell }}'
export VEDITOR='{{ default_editor.visual }}'

if [ "$(uname)" = "Darwin" ]; then
  # Suppress unwanted Homebrew-installed stuff.
  if [ -e /usr/local/share/zsh/site-functions/_git ]; then
    mv -f /usr/local/share/zsh/site-functions/{,disabled.}_git
  fi
fi

# All of our zsh files
typeset -U config_files
config_files=($DOTFILES/*/*.zsh)

# load the exports files
for file in ${(M)config_files:#*/exports.zsh}; do
  source "$file"
done

# load the path files
for file in ${(M)config_files:#*/path.zsh}; do
  source "$file"
done

# Load antibody plugins
source ~/.zsh_plugins.sh

# Load everything but the exports, path, and completion files
for file in ${${${config_files:#*/exports.zsh}:#*/path.zsh}:#*/completion.zsh}; do
  source "$file"
done

# Load completions
autoload -Uz compinit
# Only update .zcompdump once a day since this slows down the launch of a new shell session
typeset -i updated_at=$(date +'%j' -r ~/.zcompdump 2>/dev/null || stat -f '%Sm' -t '%j' ~/.zcompdump 2>/dev/null)
if [ $(date +'%j') != $updated_at ]; then
  compinit -i
else
  compinit -C -i
fi

# Load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}; do
  source "$file"
done

# Unsets the parameters since we no longer need them
unset config_files updated_at

# Use .localrc for SECRETS that you don't want in your public, versioned repo.
[ -f ~/.localrc ] && . ~/.localrc
